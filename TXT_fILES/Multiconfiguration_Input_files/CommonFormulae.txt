;===============================================================================
; COMMONFORMULAE.TXT
; Purpose: Provides a comprehensive set of reusable [System: Formula] expressions,
;          functions, and collections for Tally TDL projects, covering GST, company,
;          voucher, reporting, inventory, address, and utility logic.
;===============================================================================

;------------------------------------------------------------------------------
; REPORT VIEW AND CUSTOM REPORT LOGIC
;------------------------------------------------------------------------------

[*Line: Report View IsDefault]
    Field: Long Prompt, Report View IsDefault
    Local: Field: Long Prompt: Set as: $$LocaleString:"Set this as default view for the report"
    Option: DemoLine: Not @@IsCustomReport

[!Line: DemoLine]
    Invisible: NOT @@IsParentStandAlone OR ##DisallowDefaultView

[System: Formula]
    cwCheckReport: $$ContextKeyword:Yes
    IsCustomReport: @@cwCustomReport = @@cwCheckReport

;------------------------------------------------------------------------------
; PRODUCT/RELEASE INFO
;------------------------------------------------------------------------------

[System: Formula]
    cwTallyRelease: $$Number:$$ProdInfo:ProdReleaseAsStr

;------------------------------------------------------------------------------
; GST / COMPANY LEVEL FORMULAS
;------------------------------------------------------------------------------

[System: Formula]
    cwisGSTRelease: @@cwtallyRelease >= 1.0
    cwCMPGSTRegistrationType: $GSTRegistrationType:TaxUnit:@@CMPExcisePrimaryGodown
    cwGSTApplicablefrom: $GSTApplicableDate:TaxUnit:@@CMPExcisePrimaryGodown
    cwGSTNNumber: $GSTRegNumber:TaxUnit:@@CMPExcisePrimaryGodown
    cwcmpGSTNNumber:@@VATCMPGSTNumber
    cwPartyGSTIN: $PartyGSTIN:ledger:$partyledgername

;------------------------------------------------------------------------------
; VOUCHER/PARTY STATE & ADDRESS LOGIC
;------------------------------------------------------------------------------

[System: Formula]
    cwVoucherPartyName: If @@IsInward Then @@BuyerName Else @@ConsigneeName
    cwVoucherPartyStateName: If $$IsEmpty:$StateName OR $$IsSysName:NotApplicable:$StateName OR (@@IsOutward AND @@ICFGBuyerDetails) Then $LedStateName:Ledger:@@cwVoucherPartyName Else $StateName
    cwVoucherStateCode: If $$IsEmpty:$$getgststatecode:@@cwVoucherPartyStateName Then "" else $$getgststatecode:@@cwVoucherPartyStateName
    cwVoucherPartyStateNameAndCode: If $$IsEmpty:$$getgststatecode:@@cwVoucherPartyStateName Then @@cwVoucherPartyStateName Else @@cwVoucherPartyStateName + ", Code : " + $$getgststatecode:@@cwVoucherPartyStateName
    cwGSTVoucher: if @@cwisGSTRelease then ##varvchdate >= @@cwGSTApplicablefrom else no

;------------------------------------------------------------------------------
; ITEM, LEDGER, AND TAX DETAILS
;------------------------------------------------------------------------------

[System: Formula]
    cwitemmrprate: $(StockItem, $stockitemname).MRPDetails[Last, @@ExMRPApplicable].MRPRateDetails[1, @@ExStateWise].MRPRate
    cwItemgstrate: if not $$issysname:$stockitemname then $$GetGSTRateForInclusive:$Date:"":$StockItemName:"" ELSE 0
    cwitemHSNCode: $(StockItem, $stockitemname).GSTDETAILS[Last].HSNCODE
    cwitemHSNdesc: $(StockItem, $stockitemname).GSTDETAILS[Last].HSN
    cwledgerHSNCode: $(ledger, $ledgername).GSTDetails[Last].HSNCode
    cwGSTNatureofTransaction: $(ledger, $ledgername).GSTDetails[Last].GSTNatureofTransaction
    gstrate: $GSTDetails[Last].StatewiseDetails[1, @@IsAnyState].RateDetails[1, @@IsCGST].GSTRate
    cwIgstrate: $GSTDetails[Last].StatewiseDetails[1, @@IsAnyState].RateDetails[1, @@IsIGST].GSTRate
    cwitemgroupHSN: $(Stockgroup, $parent:stockitem:$stockitemname).GSTDETAILS[Last].HSNCODE
    cwrepItemmrp: $$number:$$GetEIStkItemMRP:$StockItemName:$Date:@locPartyStateName
    CwLastLnMRPRate: $MRPDetails[Last].MRPRateDetails[Last].MRPRate
    cwCessRate: $GSTDetails[Last].StatewiseDetails[1, @@IsAnyState].RateDetails[1, @@IsAddlCess].GSTRate
    cwgstitemrate:@@GSTItemRate
    cwVatItmrate: $(StockItem, $stockitemname).VATDetails[Last].RateofVAT

;------------------------------------------------------------------------------
; VOUCHER TYPE & LEDGER TYPE DETECTION
;------------------------------------------------------------------------------

[System: Formula]
    cwVchRcpt: $$isreceipt:$vouchertypename
    cwVchPymt: $$isPayment:$vouchertypename
    cwVchPurch: $$isPurchase:$vouchertypename
    cwVchSales: $$isSales:$vouchertypename
    cwVchDrNote: $$isDebitNote:$vouchertypename
    cwVchCrNote: $$isCreditNote:$vouchertypename
    cwvchJrnl: $$isJournal:$vouchertypename
    cwvchSO: $$isSalesOrder:$vouchertypename
    cwIsCashLedger: $$IsBelongsTo:$$GroupCash

;------------------------------------------------------------------------------
; GST LEDGER/AMOUNT EXTRACTION
;------------------------------------------------------------------------------

[System: Formula]
    cwDutyLedgerIGST: $GSTDUTYHEAD:ledger:$ledgername = "Integrated Tax"
    cwDutyLedgerCGST: $GSTDUTYHEAD:ledger:$ledgername = "Central Tax"
    cwDutyLedgerSGST: $GSTDUTYHEAD:ledger:$ledgername = "State Tax"
    cwDutyLedgerCESS: $GSTDUTYHEAD:ledger:$ledgername = "CESS"
    cwVCHIGSTVALUE: $$FilterAmtTotal:LedgerEntries:cwDutyLedgerIGST:$amount
    cwVCHCGSTVALUE: $$FilterAmtTotal:LedgerEntries:cwDutyLedgerCGST:$amount
    cwVCHSGSTVALUE: $$FilterAmtTotal:LedgerEntries:cwDutyLedgerSGST:$amount
    cwVCHCESSVALUE: $$FilterAmtTotal:LedgerEntries:cwDutyLedgerCESS:$amount
    cwVCHGSTVAlue: if not $$isempty:@@cwVCHIGSTVALUE then @@cwVCHIGSTVALUE else  @@cwVCHCGSTVALUE + @@cwVCHSGSTVALUE

;------------------------------------------------------------------------------
; DATE AND STRING UTILITIES
;------------------------------------------------------------------------------

[FUNCTION: CWDATE]
    PARAMETER: MYDATE: date: $DATE
    VARIABLE: D: NUMBER
    VARIABLE: M: NUMBER
    VARIABLE: Y: NUMBER
    VARIABLE: SEP: STRING: "-"
    10: SET: D: $$DAYOFDATE:##MYDATE
    20: SET: M: $$monthOFDATE:##MYDATE
    30: SET: Y: $$yearOFDATE:##MYDATE
    40: RETURN: $$ZEROFILL:##D:2 + ##SEP + $$ZEROFILL:##M:2 + ##SEP + $$STRING:##Y

[function: Date2YYYYMMDD]
    parameter: mf: date
    parameter: sep: string: "-"
    returns: string
    variable: s: string
    variable: yr: string: $$yearofdate:##mf
    variable: mm: number: $$monthofdate:##mf
    variable: dd: number: $$dayofdate:##mf
    10: set: s: ##yr + ##sep + $$zerofill:##mm:2 + ##sep + $$zerofill:##dd:2
    15: return: ##s

;------------------------------------------------------------------------------
; CASH/BANK OPENING AND CLOSING BALANCES
;------------------------------------------------------------------------------

[System: Formula]
    cwcashOpeningBalance: $$filteramttotal:ledger:cwCashMaster:$Openingbalance
    cwBankOpeningBalance: $$filteramttotal:ledger:cwBankMaster:$Openingbalance
    cwcashClosingBalance: $$filteramttotal:ledger:cwCashMaster:$Closingbalance
    cwBankClosingBalance: $$filteramttotal:ledger:cwBankMaster:$Closingbalance
    cwCashMaster: $$IsLedOfGrp:$Name:$$Groupcash
    cwBankMaster: $$IsLedOfGrp:$Name:$$GroupBank

;------------------------------------------------------------------------------
; USER LEVEL AND COMPANY INFO
;------------------------------------------------------------------------------

[System: Formula]
    cwNormalUser: $$cmpuserlevel <> "Owner"
    cwOwnerLevel: $$cmpuserlevel = "Owner"
    cwDataEntry: $$cmpuserlevel = "Data Entry"
    cwCmpPhone: $phonenumber:company:##svcurrentcompany
    cwCmpEmail: $EMail:company:##svcurrentcompany
    cwCmpMobile: $MobileNumbers:COMPANY:##SVCURRENTCOMPANY

;------------------------------------------------------------------------------
; PARTY, LEDGER, AND ADDRESS UTILITIES
;------------------------------------------------------------------------------

[System: Formula]
    cwpartycode: if @@cwpartycodex = $name then "" else @@cwpartycodex
    cwLEDFullAddress: $$fulllist:ledgeraddress:$address
    cwPartyEccNo: $EXCISEREGNO:ledger:$partyledgername
    cwPartyRange: $ExciseRange:ledger:$partyledgername

;------------------------------------------------------------------------------
; POS AND PAYMENT UTILITIES
;------------------------------------------------------------------------------

[System: Formula]
    cwPOSRegCard: if not $$isempty:@@cwPOSRegCardx then @@cwPOSRegCardx else 0
    cwPOSRegCardx: $$FilterValue:$Amount:LedgerEntries:1:POSCardFilter
    cwPOSRegGifVch: if not $$isempty:@@cwPOSRegGifVchx then @@cwPOSRegGifVchx else 0
    cwPOSRegGifVchx: $$FilterValue:$Amount:LedgerEntries:1:POSGiftFilter
    cwPOSRegCheque: if not $$isempty:@@cwPOSRegChequex then @@cwPOSRegChequex else 0
    cwPOSRegChequex: $$FilterValue:$Amount:LedgerEntries:Last:POSChqFilter
    cwPOSRegCash: if not $$isempty:@@cwPOSRegCashx then @@cwPOSRegCashx else 0
    cwPOSRegCashx: $$FilterValue:$Amount:LedgerEntries:1:POSCashFilter

;------------------------------------------------------------------------------
; INVENTORY AND BATCH UTILITIES
;------------------------------------------------------------------------------

[System: Formula]
    cwinvamt: $$collamttotal:inventoryentries:$amount
    cwinvAQty: $$collqTYtotal:inventoryentries:$aCTUALqTY
    cwinvbQty: $$collqTYtotal:inventoryentries:$BILLEDqTY
    cwBatchmfd: $$collectionfield:@@mfstr:first:batchallocations
    cwBatchExp: $$collectionfield:@@expstr:first:batchallocations
    CWbATCHNAME: IF $BATCHNAME = "pRIMARY BATCH" THEN "" ELSE $BATCHNAME

;------------------------------------------------------------------------------
; REPORTING AND FORMATTING
;------------------------------------------------------------------------------

[System: Formula]
    cwnextpage: $$pageno+1
    cwPrevPage: $$pageno - 1
    CWTAXCOPY: if $$SetNo = 1 then @@CWOriginal else if $$SetNo = 2 then $$LocaleString:"Duplicate - Seller's Copy" else if $$SetNo = 3 then $$LocaleString:"Triplicate - Transporter's Copy" else if $$SetNo = 4 then $$LocaleString:"Quadruplicate - Extra Copy" else $$LocaleString:"Extra Copy"
    CWOriginal: if ##SVPrintCopies > 1 then If @@IsIndianVATVch AND $IsTaxInvoice:VoucherType:$VoucherTypeName then $$LocaleString:"Original - Buyer's Copy" else $$LocaleString:"(Original)" else ""

;------------------------------------------------------------------------------
; GST/EXCISE/SERVICE TAX UTILITIES
;------------------------------------------------------------------------------

[System: Formula]
    cwIsExciseLed: $TAXTYPE:ledger:$ledgername = "excise"
    cwexciseamt: $$filtervalue:$amount:ledgerentries:1:cwbed
    cwbed: $EXCISEDUTYHEAD:ledger:$ledgername = "Basic Excise Duty"
    cwExciseCessamt: $$filtervalue:$amount:ledgerentries:1:cwExciseCess
    cwExciseCess: $EXCISEDUTYHEAD:ledger:$ledgername = "Education Cess"
    cwExciseSECCessamt: $$filtervalue:$amount:ledgerentries:1:cwExciseSECCess
    cwExciseSECCess: $EXCISEDUTYHEAD:ledger:$ledgername = "Secondary Education Cess"

;------------------------------------------------------------------------------
; COLLECTIONS FOR COMPANY, PARTY, AND ADDRESS
;------------------------------------------------------------------------------

[collection: companyaddressx]
    option: pgOldCmpAddr: not $IsMultiAddressOn:company:##svcurrentcompany or $$IsSysNameEqual:Primary:##SVAddressToPrint
    option: pgNewCmpAddr: $IsMultiAddressOn:company:##svcurrentcompany and (NOT ($$IsSysNameEqual:Primary:##SVAddressToPrint OR $$IsEmpty:##SVAddressToPrint))

[!collection: pgOldCmpAddr]
    use: companyaddress

[!collection: pgNewCmpAddr]
    use: Company Multi Address Print

[collection: cwLedgeraddress]
    type: address: ledger
    child of: $name

[collection: cwParty]
    type: address: ledger
    child of: $partyledgername

;------------------------------------------------------------------------------
; SAMPLE FORMAT PART (FOR REPORTS)
;------------------------------------------------------------------------------

[part: cwformatpart]
    line: cwformatline

[line: cwformatline]
    field: fwfc
    Local: Field: fwfc: Set As: "Sample Format"
    Local: Field: fwfc: Border: thin bottom
    Local: Field: fwfc: Style: large Bold

;------------------------------------------------------------------------------
; GST APPROPRIATE VALUE FORMULAS
;------------------------------------------------------------------------------

[System: Formula]
    cwisGSTAss: $APPROPRIATEFOR:ledger:$ledgername = "GST"
    cwInvGSTApprValue: $$FilterAmtTotal:LedgerEntries:cwisGSTAss:$amount
    cwInvGSTAssValue: $$nettamount:@@cwinvAmt:@@cwInvGSTApprValue
    cwInvGSTItemAssValue: (@@cwInvGSTAssValue / @@cwinvamt) * $amount

;===============================================================================
; END OF FILE
;===============================================================================
