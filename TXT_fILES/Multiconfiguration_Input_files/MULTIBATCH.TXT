;===============================================================================
; MULTIBATCH.TXT
; Created By: Pg on 2013-06-01 19:01, ID:
; Purpose: Enables auto-creation and filling of multiple batches in Tally
;          for stock journals, purchases, receipts, and orders, with company-level
;          serial tracking and batch quantity logic.
;===============================================================================

[System: Formula]
cwAutoCreateBatchVch : @@cwAutoCreateBatch and $cwVchHasBatchFill:Vouchertype:##SVVoucherType

;------------------------------------------------------------------------------
; VOUCHER TYPE BEHAVIOUR: AUTO FILL BATCHES OPTION
;------------------------------------------------------------------------------

[#Part: VTYP Behaviour]
    add : option : cwAutoFillBatch

[!part : cwAutoFillBatch]
    add: line : cwAutoFillBatchVch

[line : cwAutoFillBatchVch]
    field : long prompt, cwlogical
    Local: Field: cwlogical: storage: cwVchHasBatchFill
    Local: Field: long prompt: info: "Auto Fill Batches:"
    local: field: default: inactive: not ($$isstockjrnl:$parent or $$ispurchase:$parent or $$isrcptnote:$parent or $$isPurcOrder:$parent or $$issalesOrder:$parent)

[#collection : Active Batches VchExtract]
    unique : $name, $batchname

;------------------------------------------------------------------------------
; LAST CARTON SERIAL FIELD (HIDDEN, FOR SERIAL TRACKING)
;------------------------------------------------------------------------------

[LINE : cwLastCartonS]
    field: cwLastCartonS

[field : cwLastCartonS]
    use : number field
    set as : $cwLastCarton:COMPANY:##SVCURRENTCOMPANY
    skip : yes

;------------------------------------------------------------------------------
; STOCK JOURNAL: AUTO BATCH FILL LOGIC
;------------------------------------------------------------------------------

[#Part: SJ MfdBatch]
    ADD : OPTION : MYBATCHDEL : @@cwAutoCreateBatchVch

[!PART : MYBATCHDEL]
    local: line : SJ MfdBatch : delete: fields
    local: line : SJ MfdBatch : add : field : snf
    local: line : SJ MfdBatch : local: field : snf: skip : yes
    Local: Field: default: Border: thin box

[#Line: SJ MfdItem]
    option : myyskip : @@cwAutoCreateBatchvch

[!line: myyskip]
    local : field : MVCH BilledQty: read only : yes
    Local: Field: MVCH BilledQty: Set As:  $$collectionfield:($$collqtytotal:batchallocations:$billedqty):1:inventoryentriesin
    Local: Field: MVCH BilledQty: Set always : yes

[#field: MVCH BilledQty]
    sub form : cwSTKVCH BatchAllocationsFill : @@cwAutoCreateBatchvch

;------------------------------------------------------------------------------
; BATCH ALLOCATIONS FILL SUBFORM
;------------------------------------------------------------------------------

[report: cwSTKVCH BatchAllocationsFill]
    use : STKVCH BatchAllocations
    Form: STKVCH BatchAllocations
    local : Form: STKVCH BatchAllocations : add : part : at beginning : cwBatchDets1
    local : field : VCHBATCH Name : skip : not $$isempty:$$value
    local : field : VCHBATCH Name : delete : table
    local : field : VCHBATCH Name : add : table : End of list : $$line > 1 and $$issysname:$$Value
    TITLE : ##SVGodown
    LOCAL : FIELD : VCHBATCH Godown : SET by condition : $$line >1 : $$prevlinefield

[part: cwBatchDets1]
    line : cwdoFillBatches

;------------------------------------------------------------------------------
; BATCH FILL DIALOG AND LOGIC
;------------------------------------------------------------------------------

[report: cwBatchDets]
    form : cwBatchDets

[form : cwBatchDets]
    part : cwBatchDets
    on : form accept : #numf <> 0 : Call : cwAutoFillBatch:#numf:#numf2

[part : cwBatchDets]
    line : cwBatchDets, cwBatchQty

[line : cwBatchDets]
    field : sp, numf
    Local: Field: sp: info: "No. of Batches to Create:"

[line : cwBatchQty]
    field : sp, numf2
    Local: Field: sp: info: "Default Qty / Batch:"
    local: field: numf2: inactive: #numf = 0

[line : cwDoFillBatches]
    field : sp, cwlogical
    Local: Field: sp: info: "Create Batches:"
    Local: Field: cwlogical : SubForm : cwBatchDets: $$value
    Local: Field: cwlogical: Skip: not $$increatemode or $$numitems:Batchallocations > 1
    Local: field: sp: Width: 0

;------------------------------------------------------------------------------
; FUNCTION: AUTO FILL BATCHES
;------------------------------------------------------------------------------

[function : cwAutoFillBatch]
    parameter : totBatchCount : number
    parameter : batchqty : number
    variable : counter : number : 1
    variable : BatchStart : number : $cwLastCarton:COMPANY:##SVCURRENTCOMPANY + 1

    0000 : do if : $$numitems:batchallocations >1 : return : yes
    0001 : do if : ##totBatchCount = 0 : return : yes
    40 : WHILE : ##Counter <= ##totBatchCount
    50 : do if : $$loopindex > 1 : Insert Collection Object : Batch Allocations
    51 : do if : $$loopindex = 1 : set target : Batch Allocations[1]
    60 : set value : batchname : $$zerofill:##BatchStart:@@cwNumZerosBatch
    61 : incr : BatchStart
    62 : if : not $$isempty:##BatchQty
    70 : set value : ActualQty : $$asqty:##BatchQty
    80 : set value : BilledQty : $$asqty:##BatchQty
    82 : end if
    85 : incr :Counter
    90 : end while
    91 : insert collection object : batch allocations
    92 : set value : batchname : $$sysname:endoflist
    100 : return : no

;------------------------------------------------------------------------------
; UPDATE LAST SERIAL IN COMPANY ON VOUCHER ACCEPT
;------------------------------------------------------------------------------

[#form : voucher]
    on : form accept : $$increatemode and @@cwAutoCreateBatchVch : call : UpdateCountInCompany

[function : UpdateCountInCompany]
    variable : mylastbatchname : string : ""
    variable : mylastbathCtr : number
    01 : walk collection : InventoryEntriesIn
    02 : walk collection : BatchAllocations
    03 :  do if : ##mylastbatchname < $batchname : set : mylastbatchname : $batchname
    05 : end walk
    06 : end walk
    20 : set : mylastbathCtr : $$number:##mylastbatchname
    30 : do if : ##mylastbathCtr = 0 : continue
    40 : do if : ##mylastbathCtr < $cwLastCarton:COMPANY:##SVCURRENTCOMPANY : return
    50 : modify object:(company,##SVCurrentCompany).cwLastCarton[1].cwLastCarton :##mylastbathCtr

;===============================================================================
; END OF FILE
;===============================================================================
