;===============================================================================
; VOUCHERMODIFY2.TXT
; Created By: Anil on 2023-09-14 11:40, ID:
;             Taniya on 2023-08-29 17:59, ID:
; Purpose: Adds doctor selection, PCS columns, and custom rate/amount logic to
;          voucher entry and batch allocation lines for Rose Homeo scenario,
;          with advanced price level and group logic.
;===============================================================================

;------------------------------------------------------------------------------
; CONSIGNEE LINE: ADD DOCTOR SELECTION
;------------------------------------------------------------------------------

[#line: ei consignee]
    add: option: cwvcheiconsigneeoptsl: @@RoseHomeoEnabled and (@@issales or @@ispurchase or @@iscreditnote or @@isdebitnote)

[!line: cwvcheiconsigneeoptsl]
    add: RIGHT field: SP5, SNF5
    Local: Field: sp5: Set As: "Doctor :"
    Local: Field: SNF5: storage: cwdoctorname
    Local: Field: SNF5: Show table: Always
    Local: Field: SNF5: table: doctorcoll, Not Applicable
    local: field: SNF5: key: Create Ledger, Alter Ledger
    local: field: SNF5: Variable: SV Ledger
    Local: Field: snf5: Style: Normal Bold

;------------------------------------------------------------------------------
; BATCH COLUMN ONE: ADD PCS FIELD
;------------------------------------------------------------------------------

[#Line: VCHBATCH Columnone]
    add: option: cwrmvchbatchcolonepurcopt: @@RoseHomeoEnabled and (@@issales or @@ispurchase or @@iscreditnote or @@isdebitnote)

[!line: cwrmvchbatchcolonepurcopt]
    add: right fields: At Beginning: numf
    Local: Field: numf: info: "PCS"
    Local: field: numf: Width: 10
    Local: Field: numf: Style: Normal Bold
    local: field: numf: type: String
    local: field: numf: align: centre

;------------------------------------------------------------------------------
; BATCH COLUMN TWO: ADD BLANK FIELD FOR ALIGNMENT
;------------------------------------------------------------------------------

[#Line: VCHBATCH ColumnTwo]
    add: option: cwhsvchbatchcolumntwopurcopt: @@RoseHomeoEnabled and (@@issales or @@ispurchase or @@iscreditnote or @@isdebitnote)

[!line: cwhsvchbatchcolumntwopurcopt]
    add: right fields: At Beginning: numf
    Local: Field: numf: info: " "
    Local: field: numf: Width: 10
    Local: Field: numf: Style: Normal Bold
    local: field: numf: type: String
    local: field: numf: align: centre

;------------------------------------------------------------------------------
; STKVCH BATCH2: PCS, UNIT, CUSTOM AMOUNT LOGIC
;------------------------------------------------------------------------------

[#Line: STKVCH Batch2]
    add: option: cwhsstkvchbatch2purcopt: @@RoseHomeoEnabled and (@@issales or @@ispurchase or @@iscreditnote or @@isdebitnote)

[!line: cwhsstkvchbatch2purcopt]
    add: right fields: At Beginning: numf2
    add: right fields: amtf24, amtf25, amtf26
    add: right fields: after: VCHBATCH RateUnit: snfx

    Local: Field: snfx: Set As: if $$isempty:#numf2 then $baseunits:stockitem:$stockitemname else "PCS"
    Local: field: snfx: Align: centre
    Local: field: snfx: style: normal bold
    Local: field: snfx: width: 3.2
    Local: field: snfx: skip: yes
    local: field: VCHBATCH RateUnit: Invisible: yes

    Local: Field: numf2: storage: cwrmvchpcsnew
    Local: Field: VCHbatch rate: delete: set as
    Local: Field: VCHbatch rate: delete: set by condition
    Local: Field: VCHbatch rate: add: SetBy Condition: yes: if NOT $$IsEmpty:$InclusiveTaxValue then $$InclusiveTaxValue:@@ExItemRate:@@STItemRate:@@VATItemRate:@@TCSItemRate:@@GSTItemRate:@@GSTItemQtyRate:@@ExItemMRPDutyValue:@@VATItemMRPDutyValue:@@ExItemQuantum else @@cwrkvchrate

    Local: field: numf2: Width: 10
    local: field: numf2: align: centre
    Local: field: numf2: Format: "decimals:2,no zero"
    Local: Field: numf2: Inactive: @@NoBaseUnits OR @@BatchAllocBreak

    Local: Field: amtf24: Set As: (($BilledQty * $rate) - (($BilledQty * $rate) * $Discount / 100))
    Local: Field: amtf25: Set As: (($$number:#numf2 * $$number:$rate) - (($$number:#numf2 * $$number:$rate) * $Discount / 100))
    local: field: amtf25: type: number
    Local: Field: amtf26: Set As: if $$isempty:#amtf25 then #amtf24 else #amtf25

    local: field: amtf24: Invisible: yes
    local: field: amtf25: Invisible: yes
    local: field: amtf26: Invisible: yes

    Local: Field: VCHbatch Value: delete: set as
    Local: Field: VCHbatch Value: delete: set by condition
    Local: Field: VCHbatch Value: add: SetBy Condition: yes: #amtf26

;------------------------------------------------------------------------------
; EI COLUMN ONE: PCS FIELD
;------------------------------------------------------------------------------

[#Line: EI ColumnOne]
    add: option: cwhseicolumnonepurcopt: @@RoseHomeoEnabled and (@@issales or @@ispurchase or @@iscreditnote or @@isdebitnote)

[!line: cwhseicolumnonepurcopt]
    add: right fields: At Beginning: numf
    Local: Field: numf: info: "PCS"
    Local: field: numf: Width: 10
    Local: Field: numf: Style: Normal Bold
    local: field: numf: type: String
    local: field: numf: align: centre

;------------------------------------------------------------------------------
; EI COLUMN TWO: BLANK FIELD FOR ALIGNMENT
;------------------------------------------------------------------------------

[#Line: EI Columntwo]
    add: option: cwhseicomtwopurcopt: @@RoseHomeoEnabled and (@@issales or @@ispurchase or @@iscreditnote or @@isdebitnote)

[!line: cwhseicomtwopurcopt]
    add: right fields: At Beginning: numf
    Local: Field: numf: info: " "
    Local: field: numf: Width: 10
    Local: Field: numf: Style: Normal Bold
    local: field: numf: type: String
    local: field: numf: align: centre

;------------------------------------------------------------------------------
; EI INVINFO: PCS, GROUP, PRICE LEVEL, CUSTOM RATE/AMOUNT LOGIC
;------------------------------------------------------------------------------

[#line: eiinvinfo]
    add: option: cwhseiinvinfopurcopt: @@RoseHomeoEnabled and (@@ispurchase or @@iscreditnote or @@isdebitnote)

[!line: cwhseiinvinfopurcopt]
    add: field: after: VCH StockItem: snfx1, groupfield
    add: right fields: At Beginning: snf3, numf2, numf10, numf11, numf15, numf14
    add: right fields: amtf24, amtf25, amtf26
    add: right fields: after: VCH RateUnit: snfx
    add: right field: before: VCH Rate: numf20

    Local: Field: snfx: Set As: if $$isempty:#numf2 then $baseunits:stockitem:$stockitemname else "PCS"
    Local: field: snfx: Align: centre
    Local: field: snfx: style: normal bold
    Local: field: snfx: width: 3.2
    Local: field: snfx: skip: yes
    local: field: VCH RateUnit: Invisible: yes

    Local: Field: numf11: Set As: $$number:@@StdVchRate
    Local: Field: numf20: Set As: @@cwrateinpricelavel
    Local: Field: numf20: storage: cwrateinprice

    Local: Field: numf14: Set as: $$FilterValue:$cwDisc1:myCOLLStockItem2:1:celedgerdate
    Local: Field: numf15: Set As: @@cwratexxx

    Local: Field: snf3: table: myCOLLStockItem2, Not Applicable
    Local: Field: snf3: Show table: Always
    Local: Field: groupfield: Set As: $parent:stockitem:#vchstockitem

    Local: Field: VCH Discount: delete: set as
    Local: Field: VCH Discount: delete: set by condition
    Local: Field: VCH Discount: add: SetBy Condition: yes: $$number:#numf14

    Local: Field: numf2: storage: cwrmvchpcsnew
    Local: Field: numf2: Set As: $$CollectionField:$cwrmvchpcsnew:First:Batchallocations
    Local: field: numf2: Width: 10
    local: field: numf2: align: centre
    Local: field: numf2: Format: "decimals:2,no zero"
    Local: Field: numf2: Inactive: $$IsEnd:$StockItemName

    Local: Field: vch rate: delete: set as
    Local: Field: vch rate: delete: set by condition
    Local: Field: vch rate: add: SetBy Condition: yes: #numf11

    Local: Field: amtf24: Set As: (($BilledQty * $rate) - (($BilledQty * $rate) * $Discount / 100))
    Local: Field: amtf25: Set As: (($$number:#numf2 * $$number:$rate) - (($$number:#numf2 * $$number:$rate) * $Discount / 100))
    local: field: amtf25: type: number
    Local: Field: amtf26: Set As: if $$isempty:$$asamount:#amtf25 then #amtf24 else $$asamount:#amtf25

    local: field: amtf24: Invisible: yes
    local: field: amtf25: Invisible: yes
    local: field: amtf26: Invisible: yes
    local: field: numf14: Invisible: yes
    local: field: numf15: Invisible: yes
    local: field: snf3: Invisible: yes
    local: field: groupfield: Invisible: yes

    Local: Field: VCH Value: delete: set as
    Local: Field: VCH Value: delete: set by condition
    Local: Field: VCH Value: add: SetBy Condition: yes: #amtf26

    Local: Field: numf10: Set As: $$number:#VCHBilledQty / $$number:#numf2
    Local: Field: numf10: storage: cwrmvchpcsnew5
    Local: Field: numf10: Skip: Yes
    Local: Field: snfx1: Skip: Yes
    Local: Field: groupfield: Skip: Yes
    Local: field: numf10: Align: centre
    Local: Field: numf10: Style: Normal Bold

[System: Formula]
    cwrkvchrate: If NOT $$IsRemoteCompany Then If $$Isstartupvalue Then $$Value Else ($$CurrentValue:$StandardPrice:StockItem:$StockItemName) Else If $$Isstartupvalue Then $$Value Else ($CurrentStdPriceMethod:StockItem:$StockItemName)

;------------------------------------------------------------------------------
; ADDITIONAL COLLECTIONS, VARIABLES, AND FORMULAS
;------------------------------------------------------------------------------

[Collection: myCOLLStockItem2x]
    type: stock group
    filter: cwitemgroupfilter

[System: Formula]
    cwitemgroupfilter: ##groupfield = $name
    cwconfigpricelevel: $UsePriceLevels:COMPANY:##SVCURRENTCOMPANY

[Collection: myCOLLStockItem2]
    source collection: myCOLLStockItem2x
    walk: colldatepriselevel, collpriselevel
    compute: cwpricelavelx1: $cwpricelavelx
    compute: cwDate1: $cwDate
    compute: cwfromdatestrg1: $cwfromdatestrg
    compute: cwFrom1: $cwFrom
    compute: cwLessThen1: $cwLessThen
    compute: cwRate1: $cwRate
    compute: cwDisc1: $cwDisc
    compute: name1: $name
    add: format: $cwpricelavelx, 10
    add: format: $cwDate1, 10
    add: format: $cwfromdatestrg1, 10
    add: format: $cwFrom1, 10
    add: format: $cwLessThen1, 10
    add: format: $cwRate1, 10
    add: format: $cwDisc1, 10
    add: format: $name1, 10
    fetch: cwpricelavelx, cwDate, cwfromdatestrg, cwFrom, cwLessThen, cwRate, cwDisc, name
    filter: celedgerdate2
    filter: cwitemgroupfilterxx

[System: Formula]
    celedgerdate: #groupfield = $name1
    celedgerdate2: #VCHBilledQty >= $cwFrom and #VCHBilledQty <= $cwLessThen

;------------------------------------------------------------------------------
; FIELD DEFINITIONS FOR GROUPFIELD
;------------------------------------------------------------------------------

[field: groupfield]
    Use: name field
    Modifies: groupfield: yes

[Variable: groupfield]
    Persistent: Yes
    Type: string

[system: variable]
    groupfield: ""

;===============================================================================
; END OF FILE
;===============================================================================
