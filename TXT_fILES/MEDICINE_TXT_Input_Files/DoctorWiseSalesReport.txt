;===============================================================================
; DOCTORWISESALESREPORT.TXT
; Created By: Anil on 2023-09-28 12:49, ID:
; Purpose: Doctor-wise sales report in Tally, with voucher, party, product,
;          quantity, taxable value, GST, and total calculations.
;===============================================================================

;------------------------------------------------------------------------------
; MENU INTEGRATION (OPTIONAL)
;------------------------------------------------------------------------------

[#menu: Gateway of Tally]
;; {29.Sep.23 18:03} add: Option: DoctorWiseSalesReportLock ;; : @@DoctorWiseSalesReportDemoLock

[#menu : cw_Debug_menu]
    add: Item: before: @@locQuit: @@DoctorWiseSalesReportReport: Display: RepDoctorWiseSalesReport

[!menu: DoctorWiseSalesReportLock]
    add: Item: before: @@locQuit: @@DoctorWiseSalesReportReport: Display collection:ListofExtractLedgersDoctorx
    add: Item: before: @@locQuit: Blank

;------------------------------------------------------------------------------
; DOCTOR LEDGER SELECTION COLLECTION
;------------------------------------------------------------------------------

[Collection: ListofExtractLedgersDoctorx]
    Use              : Extract Alias Collection
    Source Collection: List of Ledgers
    Title            : $$LocaleString:"List of Doctor"
    Report           : RepDoctorWiseSalesReport
    Variable         : SLedger
    Trigger          : SDoctor
    Fetch            : Name

;------------------------------------------------------------------------------
; SYSTEM FORMULAS
;------------------------------------------------------------------------------

[System: formula]
DoctorWiseSalesReportReport: "Doctor Wise Sales Report"
;; DoctorWiseSalesReportDemoLock: $$MachineDate < $$Date:"01/04/2013"

;------------------------------------------------------------------------------
; MAIN REPORT AND FORM
;------------------------------------------------------------------------------

[Report: RepDoctorWiseSalesReport]
    Use      : Dsp Template
    Use      : Range Template
    Title    : @@DoctorWiseSalesReportReport
    Printset : Report Title: @@DoctorWiseSalesReportReport
    Form     : FrmDoctorWiseSalesReport
    Export   : Yes
    Set      : svfromdate : ##svcurrentdate
    Set      : svTodate   : ##svcurrentdate
    Local    : Button : RelReports : Inactive : Yes

[Form: FrmDoctorWiseSalesReport]
    Use      : DSP Template
    Part     : DspAccTitles, PrtTitle0DoctorWiseSalesReport, PrtDoctorWiseSalesReport
    Width    : 100% Page
    Height   : 100% Page
    Delete   : page break
    Add      : page break: DoctorWiseSalesReportbotbrk, DoctorWiseSalesReportbotOpbrk
    Bottom Toolbar Buttons: BottomToolBarBtn1, BottomToolBarBtn3, BottomToolBarBtn8, BottomToolBarBtn9, BottomToolBarBtn10, BottomToolBarBtn11, BottomToolBarBtn12
    Add      : button: Save View, change view
    Option   : enable Range filters

[part: DoctorWiseSalesReportbotBrk]
    line  : EXPINV PageBreak
    border: thin top

[part: DoctorWiseSalesReportbotopbrk]
    use: dspacctitles
    add: part: DoctorWiseSalesReportTitlePart

[part: DoctorWiseSalesReportTitlePart]
    line: LnDoctorWiseSalesReportTitle

[line: LnDoctorWiseSalesReportCurrPeriod]
    field: fwf, fwf2
    Local: field: fwf2: Align: Right
    Local: Field: fwf: Style: normal bold
    Local: Field: fwf2: Style: normal bold
    Local: Field: fwf2: Set As: @@dspDateStr
    invisible: $$inprintmode

[part: PrtTitle0DoctorWiseSalesReport]
    line : LnDoctorWiseSalesReportCurrPeriod

[Part: PrtDoctorWiseSalesReport]
    Line       : LnDoctorWiseSalesReportTitle, LnDoctorWiseSalesReport
    bottom Line: LnDoctorWiseSalesReportTotals
    repeat     : LnDoctorWiseSalesReport: ColDoctorWiseSalesReport
    scroll     : Vertical
    Common Border: Yes
    Total      : Qtyf, amtf, amtf3, amtf4

;------------------------------------------------------------------------------
; DATA COLLECTIONS
;------------------------------------------------------------------------------

[Collection: ColDoctorWiseSalesReport]
    source collection: ColDoctorsalesreportsrcx
    walk : inventoryentries
    by   : date : $date
    by   : vouchernumber : $vouchernumber
    by   : partyledgername : $partyledgername
    by   : doctorx : $..cwdoctorname
    by   : masterid : $masterid
    compute : cwVCHCGSTVALUE2xx : $cwVCHCGSTVALUE2x
    compute : cwVCHSGSTVALUE2xx : $cwVCHSGSTVALUE2x
    compute : Invtotal : $$CollAmtTotal:inventoryentries:$amount
    by   : parent : $cwTopMostGroup:stockgroup:$parent:stockitem:$stockitemname
    aggr compute : billedqty : sum : $billedqty
    aggr compute : amount : sum : $amount
    filter : ColDoctorsalesreportfilterx

[Collection: ColDoctorsalesreportsrcx]
    Use    : Vouchers of Company
    compute : cwVCHCGSTVALUE2x : @@cwVCHCGSTVALUE2
    compute : cwVCHSGSTVALUE2x : @@cwVCHSGSTVALUE2
    delete : filter : daybookfilter
    Filter : ColDoctorsalesreportsrcFilterx, IsNonOptionalCancelledVchs

[system: Formula]
ColDoctorsalesreportsrcFilterx: if $$issales:$vouchertypename then $cwdoctorname=##sledger else no
ColDoctorsalesreportfilterx   : $doctorx=##sledger

;------------------------------------------------------------------------------
; TITLE LINE: COLUMN HEADERS
;------------------------------------------------------------------------------

[Line: LnDoctorWiseSalesReportTitle]
    use   : LnDoctorWiseSalesReport
    option: titleopt
    local: field: snf: set as: "Vch No"
    local: field: sdf: set as: "Vch Date"
    local: field: nf: set as: "Party Name"
    local: field: fwf: set as: "Party Name"
    local: field: qtyf: set as: "Product Qty."
    local: field: amtf: set as: "Taxable Value"
    local: field: amtf1: set as: "CGst"
    local: field: amtf2: set as: "SGST"
    local: field: amtf3: set as: "GST"
    local: field: amtf4: set as: "Total Vch Amount"
    local: field: amtf5: set as: "Total Vch Amount"
    local: field: ratepf : set as : "Rate"
    local: field: default : style: normal bold

    Local: field: snf: Align: centre
    Local: field: sdf: Align: centre
    Local: field: nf: Align: centre
    Local: field: qtyf: Align: centre
    Local: field: amtf: Align: centre
    Local: field: amtf1: Align: centre
    Local: field: amtf2: Align: centre
    Local: field: amtf3: Align: centre
    Local: field: amtf4: Align: centre
    Local: field: ratepf: Align: centre
    local: field: fwf: indent: 15

;------------------------------------------------------------------------------
; MAIN DATA LINE: SALES DETAILS
;------------------------------------------------------------------------------

[Line: LnDoctorWiseSalesReport]
    Fields: snf, sdf, fwf
    right field: Qtyf, ratepf, Amtf, amtf1, amtf2, amtf3, amtf4, amtf5
    local: field: qtyf : Format : "NoSymbol, Short Form, No Compact,NoZero"
    local: field: ratepf : setas  : #amtf/#qtyf
    local: field: fwf : alter : voucher : $$isvoucher
    local: field: snf: set as: $vouchernumber
    local: field: sdf: set as: $date
    local: field: nf: set as: $partyledgername
    local: field: fwf: set as: $partyledgername
    local: field: qtyf: set as: $billedqty
    local: field: amtf: set as: $amount
    local: field: amtf1: set as: $cwVCHCGSTVALUE2xx
    local: field: amtf2: set as: $cwVCHSGSTVALUE2xx
    local: field: amtf3: set as: if $$line=1 then #amtf1+#amtf2 else if $vouchernumber <> $$prevobj:$vouchernumber then #amtf1+#amtf2 else ""
    Local: field: amtf4: Set As: $Invtotal
    Local: field: amtf5: Set As: if $$line=1 then #amtf3+#amtf4 else if $vouchernumber <> $$prevobj:$vouchernumber then #amtf3+#amtf4 else ""
    Local: Field: default: Border: thin right

    local: field: amtf1: Invisible: yes
    local: field: amtf2: Invisible: yes
    local: field: amtf4: Invisible: yes
    border: thin top

    add: option : cwalterVch

[!line : cwalterVch]
    Key: CW Alter On Enter, CW Alter on LDC

[Key: CW Alter On Enter]
    Key    : Enter
    Action : Call : CW Alter On Enter

[Key: CW Alter on LDC]
    Key    : Left Double Click
    Action : Call : CW Alter On Enter

[Function: CW Alter On Enter]
    VARIABLE : VCHID : STRING
    00   : SET : VCHID : $$SPrintf:"ID:%s":$masterid
    000  : SET OBJECT : (Voucher, ##VchId).
    0002 : alter : myvoucher

[report : myvoucher]
    use : voucher
    form : voucher
    OBJECT : VOUCHER : ##VCHID

;------------------------------------------------------------------------------
; TOTALS LINE
;------------------------------------------------------------------------------

[line: LnDoctorWiseSalesReportTotals]
    use   : LnDoctorWiseSalesReport
    option: totalOpt
    local: field: fwf: align: right
    local: field: default : style: normal bold
    local: field: qtyf: set as: $$total:qtyf
    local: field: fwf: set as: "Total"
    local: field: fwf: set as: ""
    local: field: ratepf: set as: ""
    local: field: amtf : set as :  $$total:amtf
    local: field: amtf3 : set as :  $$total:amtf3
    local: field: amtf4 : set as :  $$total:amtf4

;===============================================================================
; END OF FILE
;===============================================================================
