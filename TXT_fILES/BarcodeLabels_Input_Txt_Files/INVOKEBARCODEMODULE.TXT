;===============================================================================
; INVOKEBARCODEMODULE.TXT
; Created By: pg on 2012-02-11 16:41
; Purpose: Adds a "BarCode" button to Voucher entry and display screens in Tally,
;          allowing users to trigger an external barcode module/app with voucher details.
;          The button executes a command with voucher GUID, date, and type as parameters.
;===============================================================================

;------------------------------------------------------------------------------
; Add BarCode button option to Voucher form if customization is enabled
;------------------------------------------------------------------------------

[#form : Voucher]
add : option : BarCodebuttonOpt : @@cwCustomizationEnabled

[!form : BarCodebuttonOpt]
add : button : BarCodeButton
on : form accept : @@GenBarCodeOnSave : exec command : @@tallysmsclient  : @@paramstr

;------------------------------------------------------------------------------
; Add BarCode button option to Voucher Display form if customization is enabled
;------------------------------------------------------------------------------

[#Form : VCHDisplay]
add : option : BarCodebuttonOpt : @@cwCustomizationEnabled

[#Report: VCH Display]
Object : Voucher : ##VoucherID

;------------------------------------------------------------------------------
; Define BarCode button (Alt+B) to execute external barcode module/app
;------------------------------------------------------------------------------

[key : BarCodeButton]
title : "BarCode"
key : Alt + B
action : exec command : @@tallysmsclient  : @@paramstr
;inactive : not @@GetBarcodeBtnStatus

;------------------------------------------------------------------------------
; Function for logging/debugging (returns the input string)
;------------------------------------------------------------------------------

[function : cwx]
parameter : str : string
10 : log : ##str
20 : return : ##str

;------------------------------------------------------------------------------
; System formulas for parameter string and barcode client location
;------------------------------------------------------------------------------

[System: Formula]
; Construct parameter string: GUID + delimiter + date + delimiter + voucher type
paramstr : @@cwxguid+@@dlr+@@xxdate+@@dlr+$vouchertypename

; Get GUID for the voucher (if masterid is 0, use current voucher ID)
cwxguid : if $masterid = 0 then $$string:@@CurrVchId else $$string:$masterid

; Path to the barcode client application (from company config)
tallysmsclient : @@cwAppLocation

; Voucher date in short date format
xxdate : $$string:$date:"shortdate"

;------------------------------------------------------------------------------
; (Optional) Formula to control button status based on voucher type
;------------------------------------------------------------------------------

GetBarcodeBtnStatus : if @@BarCodeInPurchase and $$ispurchase:##SVVouchertype then yes else if @@BarCodeInSales and $$issales:##svvouchertype +
then yes else if @@BarCodeInStkJrnl and $$isstockjrnl:##Svvouchertype then yes else no
