;===============================================================================
; VCHTYPECHANGES.TXT
; Created By: Suman on 2016-11-08 14:56, ID:
; Purpose: Adds barcode-related options and custom behaviors to Voucher Type
;          configuration in Tally, including barcode entry, rate/amount skipping,
;          duplicate barcode lock, and POS-like skipping.
;===============================================================================

;------------------------------------------------------------------------------
; Add barcode and related options to Voucher Type Behaviour part
;------------------------------------------------------------------------------

[#Part: VTYP Behaviour]
add:option :VTYPBehavioursl:@@forslvtyp and @@cwenablebarcodebatch

[!Part: VTYPBehavioursl]
add:line:lnvtypacceptbarcode,lnvtypacceptbarcode2,skipamtrateline,cwSkipOtherDetails,barcodeduplicaoptline

;------------------------------------------------------------------------------
; Option: Accept Barcode Before Item?
;------------------------------------------------------------------------------

[Line: lnvtypacceptbarcode]
Fields      : Long Prompt, cwlogical
Local       : Field : Long Prompt : Info : $$LocaleString:"Accept Barcode Before Item ?"
Local: Field:cwlogical : storage:cwvtypbarcodeitem
Space Top   : 0.5
Local: Field: default: Style: Normal Bold

;------------------------------------------------------------------------------
; Option: Accept Items also? (if barcode before item is enabled)
;------------------------------------------------------------------------------

[line : lnvtypacceptbarcode2]
use : lnvtypacceptbarcode
Local: Field : Long Prompt : Info : $$LocaleString:"Accept Items also ?"
Local: Field:cwlogical : storage:cwvtypbarcodeitemwobatch
local: field: cwlogical: inactive: not $cwvtypbarcodeitem

;------------------------------------------------------------------------------
; Option: Skip Rate & Amount?
;------------------------------------------------------------------------------

[line:skipamtrateline]
field:sp,cwlogical
Local: Field: sp: Set As:"Skip Rate & Amount?"
Local: Field: cwlogical: storage:cwskiprateamt
Local: field: sp: Width:35
local: field: cwlogical: inactive: not $cwvtypbarcodeitem

;------------------------------------------------------------------------------
; Option: Skip Like POS?
;------------------------------------------------------------------------------

[line : cwSkipOtherDetails]
field : sp,cwlogical
Local: Field: sp: info: "Skip Like POS?"
Local: Field: cwlogical: storage: cwSkipLikePOS
Space Top   : 0.5
Local: field: sp: Width:35

;------------------------------------------------------------------------------
; Option: Lock Duplicate Barcode Entry?
;------------------------------------------------------------------------------

[line:barcodeduplicaoptline]
field:sp,cwlogical
Local: Field: sp: Set As:"Lock Duplicate Barcode Entry?"
Local: Field: cwlogical: storage:cwbarcodedupopt
Local: field: sp: Width:35
local: field: cwlogical: inactive: not $cwvtypbarcodeitem

;------------------------------------------------------------------------------
; System formulas for enabling options and behaviors
;------------------------------------------------------------------------------

[System: Formula]
cwenableskipopt     : $cwskiprateamt:vouchertype:$VOUCHERTYPENAME
cwbarcodedupoptenb  : $cwbarcodedupopt:vouchertype:$VOUCHERTYPENAME

;; {06.Dec.23 11:42}     forslvtyp : $$issales:$parent or $$isdelnote:$parent or $$isdebitnote:$parent or $$iscreditnote:$parent   ;;or $$isdelnote:$parent
forslvtyp : $$issales:$parent or $$isdelnote:$parent or $$isdebitnote:$parent or $$iscreditnote:$parent or $$IsJobMaterialIssue:$parent
cwvtypbarcodeitem : $cwvtypbarcodeitem:Vouchertype:##SVVoucherType
